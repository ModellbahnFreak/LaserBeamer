plugins {
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

group 'de.modellbahnfreak.laserbeamer'
version '1.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}
dependencies {

}

javafx {
    version = "17"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.media' ]
}

tasks.withType(JavaCompile) {
    options.release = 17
}

jar {
    manifest {
        attributes 'Main-Class': 'de.modellbahnfreak.laserbeamer.server.Server'
    }
}

task fatJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest.from jar.manifest
    classifier = 'all'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

task packageZip(type: Zip) {
    archiveFileName = 'LaserBeamer-' + version + '.zip'
    destinationDirectory = layout.buildDirectory.dir("libs")
    from (layout.buildDirectory.dir("libs")) {
        include("*-all.jar")
    }
    from (layout.projectDirectory.dir("scripts")) {
        include("*")
    }
    from (layout.projectDirectory.dir("gobos")) {
        into("gobos")
    }
    from (layout.projectDirectory) {
        include("settings.ini")
        include("S_L LaserBeamer Software.xml")
    }
}